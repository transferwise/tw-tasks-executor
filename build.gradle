import org.eclipse.jgit.api.errors.RefAlreadyExistsException

buildscript {
    dependencies {
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.15.2"
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${System.getenv("SPRING_BOOT_VERSION") ?: '2.6.5'}")
    }
}

plugins {
    id "com.github.spotbugs" version "5.0.6" apply false
    id "idea"
    id 'org.ajoberstar.grgit' version '4.1.1'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
}

idea.project {
    vcs = 'Git'
    languageLevel = JavaVersion.VERSION_11
    targetBytecodeVersion = JavaVersion.VERSION_11
}

idea.module {
    downloadJavadoc = false
    downloadSources = true
    excludeDirs += file('logs')
    excludeDirs += file('gradle')
    excludeDirs += file('build')
    excludeDirs += file('target')
    excludeDirs += file('.idea')
    excludeDirs += file('.gradle')
    excludeDirs += file('logs2')
}

task tagRelease {
    doLast {
        try {
            grgit.tag.add {
                name = "v$version"
                message = "Release of ${version}"
            }
            grgit.push(refsOrSpecs: ["v$version"])
        }
        catch (RefAlreadyExistsException ignored) {
            logger.warn("Tag v$version already exists.")
        }
    }
}

group = "com.transferwise.tasks"

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USER")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

tasks.findByName("initializeSonatypeStagingRepository").setOnlyIf {
    System.getenv("OSS_SIGNING_KEY")
}
