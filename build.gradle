import org.eclipse.jgit.api.errors.RefAlreadyExistsException

buildscript {
    if (!project.hasProperty("springBootVersion")) {
        ext.springBootVersion = System.getenv("SPRING_BOOT_VERSION") ?: "3.2.2"
    }
    dependencies {
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.16.4"
    }
}

plugins {
    id "com.github.spotbugs" version "5.0.14" apply false
    id 'org.springframework.boot' version "$springBootVersion" apply false
    id "idea"
    id 'org.ajoberstar.grgit' version '5.2.0'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"

}

idea.project {
    vcs = 'Git'
    languageLevel = JavaVersion.VERSION_17
    targetBytecodeVersion = JavaVersion.VERSION_17
}

idea.module {
    downloadJavadoc = false
    downloadSources = true
    excludeDirs += file('logs')
    excludeDirs += file('gradle')
    excludeDirs += file('build')
    excludeDirs += file('target')
    excludeDirs += file('.idea')
    excludeDirs += file('.gradle')
    excludeDirs += file('logs2')
}

task tagRelease {
    doLast {
        try {
            grgit.tag.add {
                name = "v$version"
                message = "Release of ${version}"
            }
            grgit.push(refsOrSpecs: ["v$version"])
        }
        catch (RefAlreadyExistsException ignored) {
            logger.warn("Tag v$version already exists.")
        }
    }
}

group = "com.transferwise.tasks"

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USER")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

tasks.findByName("initializeSonatypeStagingRepository").setOnlyIf {
    System.getenv("OSS_SIGNING_KEY")
}
